import java.text.SimpleDateFormat

plugins {
  id 'java-library'
  id 'org.jetbrains.kotlin.jvm'
  id 'net.nemerosa.versioning' version '2.6.1'
}

group 'io.redgreen'
version '1.0-SNAPSHOT'

repositories {
  mavenCentral()
}

jar {
  manifest {
    attributes(
        'Generator'      : 'com.example.generator.LibraryProjectGenerator',
        'Built-By'       : System.properties['user.name'],
        'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
        'Build-Revision' : versioning.info.commit,
        'Created-By'     : "Gradle ${gradle.gradleVersion}",
        'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
        'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
    )
  }
}

dependencies {
  implementation project(':fluid:api')
  implementation artefacts.kotlin.jdkStdlib

  testImplementation project(':fluid:generator-testing')
  testImplementation artefacts.junit.api
  testRuntimeOnly artefacts.junit.runtime
  testImplementation artefacts.truth
}

compileKotlin {
  kotlinOptions.jvmTarget = versions.jvm
}

compileTestKotlin {
  kotlinOptions.jvmTarget = versions.jvm
}

test {
  useJUnitPlatform()
}
